apply plugin: 'groovy'
apply plugin: 'maven'
apply plugin: 'maven-publish'


String classPath = ".src.main.groovy.com.froyo.replugin.plugin.gradle.androidx.AppConstant".replace(".", java.io.File.separator)
String verPath = "${project.projectDir}" + classPath + ".groovy"
String verLine = new File(verPath).filterLine { it =~ /def static final VER =/ }
version = "${verLine.split("\"")[1]}" // 版本
//红色醒目打印显示版本号
System.err.println "version=${version}"

dependencies {
    implementation 'com.android.tools.build:gradle:3.5.3'
    implementation 'org.json:json:20160212'
    implementation 'org.codehaus.groovy:groovy:2.4.7'
    implementation 'com.squareup:javapoet:1.5.1'
    implementation 'com.android.tools.build:transform-api:1.5.0'
    implementation 'javassist:javassist:3.12.1.GA'
    implementation 'commons-io:commons-io:2.5'

    implementation gradleApi()
    implementation localGroovy()
}

repositories {
    google()
    jcenter()
}

if (project.hasProperty("android")) { // Android libraries
    task sourcesJar(type: Jar) {
        classifier = 'sources'
        from android.sourceSets.main.java.srcDirs
    }

    task javadoc(type: Javadoc) {
        source = android.sourceSets.main.java.srcDirs
        classpath += project.files(android.getBootClasspath().join(File.pathSeparator))
    }
} else { // Java libraries
    task sourcesJar(type: Jar, dependsOn: classes) {
        classifier = 'sources'
        from sourceSets.main.allSource
    }
}

// 强制 Java/JavaDoc 等的编码为 UTF-8
tasks.withType(JavaCompile) {
    options.encoding = "UTF-8"
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}



// add javadoc/source jar tasks as artifacts
artifacts {
    archives sourcesJar, javadocJar
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            if (plugins.hasPlugin('war')) {
                from components.web
            } else {
                from components.java
            }

            artifact sourcesJar
            artifact javadocJar
        }
    }
}

project.ext.RP_ARTIFACT_ID = 'replugin-plugin-gradle-androidx'
apply from: '../rp-publish.gradle'